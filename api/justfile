set fallback

default:
    @just --list --justfile {{justfile()}} --list-heading $'\nAPI project recipes:\n'
    @just list-commands

# builds js files that can run on an unraid server
@build:
    pnpm run build

# deploys to an unraid server
@deploy remote:
    ./scripts/deploy-dev.sh {{remote}}

# watches typescript files and restarts dev server on changes
@watch:
    watchexec -e ts -r -- pnpm dev

alias b := build
alias d := deploy
alias w := watch

sync-env server:
    rsync -avz --progress --stats -e ssh .env* root@{{server}}:/usr/local/unraid-api
    ssh root@{{server}} 'cp /usr/local/unraid-api/.env.staging /usr/local/unraid-api/.env'
